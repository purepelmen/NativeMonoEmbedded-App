plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'com.monoembedtest.nativeapp'
    compileSdk 34

    defaultConfig {
        applicationId "com.monoembedtest.nativeapp"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "arm64-v8a", "x86_64"
        }
    }
    sourceSets {
        main {
            assets.srcDirs = [
                'src/main/assets',

                // Mono Runtime specific files are included from separate generated folders (excluded from Git).
                'src/main/assetsGen',
                "${rootProject.projectDir}/../thirdparty/Runtime.Mono/gen-android/dist"
            ]
            jniLibs.srcDirs = [
                'src/main/jniLibs',

                // Mono Runtime specific libraries are included from a separate generated folder.
                "${rootProject.projectDir}/../thirdparty/Runtime.Mono/gen-android/jniLibs"
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

// Our CMake knows when and where to look for required files that's need to be packaged in the APK, so please execute it before the assets will be merged.
project.afterEvaluate {
    mergeDebugAssets.dependsOn externalNativeBuildDebug
    mergeReleaseAssets.dependsOn externalNativeBuildRelease
}

dependencies {
}